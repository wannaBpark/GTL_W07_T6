// light.hlsl

#define MAX_LIGHTS 16 

#define POINT_LIGHT			1
#define SPOT_LIGHT			2

struct LIGHT
{
    float3 m_cAmbient;
    float pad1;

    float3 m_cDiffuse;
    float pad2;

    float3 m_cSpecular;
    float pad3;

    float3 m_vPosition;
    float m_fFalloff;

    float3 m_vDirection;
    float pad4;

    float m_fAttenuation;
    int m_bEnable;
    int m_nType;
    float m_fRange;
};

cbuffer cbLights : register(b2)
{
    LIGHT gLights[MAX_LIGHTS];
    float4 gcGlobalAmbientLight;
    int gnLights;
    float3 padCB;
};

float4 SpotLight(int nIndex, float3 vPosition, float3 vNormal)
{
    // 광원과 픽셀 위치 간 벡터 계산
    float3 vToLight = gLights[nIndex].m_vPosition - vPosition;
    float fDistance = length(vToLight);
    
    if (fDistance <= gLights[nIndex].m_fRange)
    {
        float fSpecularFactor = 0.0f;
        vToLight /= fDistance; // 정규화
        
        float fDiffuseFactor = saturate(dot(vNormal, normalize(vToLight)));

        if (fDiffuseFactor > 0.0f)
        {
            float3 vView = normalize(CameraPosition - vPosition);
            float3 vHalf = normalize(vToLight + vView);
            fSpecularFactor = pow(max(dot(normalize(vNormal), vHalf), 0.0f), 1);
        }
        
        float fSpotFactor = pow(max(dot(-vToLight, gLights[nIndex].m_vDirection), 0.0f), gLights[nIndex].m_fFalloff);
        
        float fAttenuationFactor = 1.0f / (1.0f + gLights[nIndex].m_fAttenuation * fDistance * fDistance);
     
       
        
        float3 lit = (gLights[nIndex].m_cAmbient.rgb * Material.AmbientColor.rgb) +
                     (gLights[nIndex].m_cDiffuse.rgb * fDiffuseFactor * Material.DiffuseColor) +
                     (gLights[nIndex].m_cSpecular * fSpecularFactor * Material.SpecularColor);

        return float4(lit * fAttenuationFactor * fSpotFactor, 1.0f);
    }
    return float4(0.0f, 0.0f, 0.0f, 0.0f);
}

float4 PointLight(int nIndex, float3 vPosition, float3 vNormal)
{
    float3 vToLight = gLights[nIndex].m_vPosition - vPosition;
    float fDistance = length(vToLight);
    if (fDistance <= gLights[nIndex].m_fRange)
    {
        float fSpecularFactor = 0.0f;
        vToLight /= fDistance;
        float fDiffuseFactor = saturate(dot(vNormal, normalize(vToLight)));

        if (fDiffuseFactor > 0.0f)
        {
            float3 vView = normalize(CameraPosition - vPosition);
            float3 vHalf = normalize(vToLight + vView);
            fSpecularFactor = pow(max(dot(normalize(vNormal), vHalf), 0.0f), 1);
            
        }

        float fAttenuationFactor = 1.0f / (1.0f + gLights[nIndex].m_fAttenuation * fDistance * fDistance);
   
        float3 lit = (gLights[nIndex].m_cAmbient.rgb * Material.AmbientColor.rgb) +
                     (gLights[nIndex].m_cDiffuse.rgb * fDiffuseFactor * Material.DiffuseColor) +
                     (gLights[nIndex].m_cSpecular * fSpecularFactor * Material.SpecularColor);

        return float4(lit * fAttenuationFactor, 1.0f);
    }
    return (float4(0.0f, 0.0f, 0.0f, 0.0f));
}


float4 Lighting(float3 vPosition, float3 vNormal)
{
    
    float4 cColor = float4(0.0f, 0.0f, 0.0f, 0.0f);
      [unroll(MAX_LIGHTS)]
    for (int i = 0; i < gnLights; i++)
    {
        if (gLights[i].m_bEnable)
        {
            if (gLights[i].m_nType == POINT_LIGHT)
            {
                cColor += PointLight(i, vPosition, vNormal);
            }
            else if (gLights[i].m_nType == SPOT_LIGHT)
            {
                cColor += SpotLight(i, vPosition, vNormal);
            }
        }
    }
    
    // 전역 환경광 추가
    cColor += (gcGlobalAmbientLight);
    cColor.a = 1;
    
    return cColor;

}
